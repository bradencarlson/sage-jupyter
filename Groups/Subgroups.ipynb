{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Subgroups and Cyclic Groups"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Here we will go over how to define subgroups and cyclic subgroups of the groups that we have already seen.  One way to generate a subgroup of a group is to specify the elements of the subgroup.  For example, consider the group $S_5$, we can define a subgroup of $S_5$, by writing the following:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "S5 = SymmetricGroup(5)\n",
    "\n",
    "sigma = S5('(4,5)')\n",
    "tau = S5('(1,3)')\n",
    "mu = S5('(1,3)(4,5)')\n",
    "I = S5('()')\n",
    "\n",
    "G = S5.subgroup([I,sigma,tau,mu])"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "We do need to be sure that we got every member of the group, or at the very least a complete set of generators for this subgroup (in the example above we have every member of the subgroup, as we will soon see), because what this `.subgroup()` function is actually doing is generating the cyclic subgroup generated by the list of elements given as the parameter.  So, in the example above, since these four elements are closed under composition, they form a subgroup of $S_5$.  We can see the complete list of all the elements of the subgroup, and some other atributes in the code below."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "print(G.list())\n",
    "print()\n",
    "print(f\"The order of G is {G.order()}\")\n",
    "print(f\"Is G abelian? {G.is_abelian()}\")\n",
    "print(G.cayley_table())"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "One thing the we forgot to mention about those Cayley tables that Sage can produce so nicely for us, while the table itself does not tell us which elements are which (although its not too hard to spot the identity), we can have SageMath print out the values of each column and row label.  For example, say we wanted to know which element SageMath means when it says $b*c=d$ above.  Noting that lists start at position zero, and thus $b$ is in row 1, and $c$ is in column 2, we would type this:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "T = G.cayley_table()\n",
    "rows = T.row_keys()\n",
    "columns = T.column_keys()\n",
    "\n",
    "print(f\"b={rows[1]}\")\n",
    "print(f\"c={columns[2]}\")\n",
    "print(f\"d={rows[3]}\")"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Or, maybe this is easier, we could simply type:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "sorted(T.translation().items())"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Alternatively, we could use the `change_names()` function to rename the lables for the Cayley table. Once again we need to be careful here, as we may not necessarily know what order SageMath has the elements (although usually it makes sense the order they are in).  Here we relable the elements to be their cycle notation representation, then we call the `.translation().items()` functions to make sure that each element is matched up correctly, then we print out the Cayley table again, this time knowing exactly which elements are which.  \n",
    "\n",
    "See the last example for an example of code that will generate the list of names in the order we want for the cayley table automatically, then assignes those names to the Cayley table.  "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "T.change_names(names=['1','(4,5)','(1,3)','(1,3)(4,5)'])\n",
    "print(sorted(T.translation().items()))\n",
    "T"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "As we mentioned before, what SageMath does when we call the `.subgroup()` function is generate the cyclic subgroup based on a list of generators that we pass in as a parameter.  So the following code generates the same subgroup, $G$, that we have been working with up until now, becuase we can find two elements that generate the subgroup $G$. "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "H = S5.subgroup(['(1,3)','(4,5)'])\n",
    "\n",
    "print(H.list())\n",
    "print(G.list())"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Here we go through another example, this time making use of a `for` loop to iterate through each element in the subgroup, to reassign it's name in the Cayley table.  A word of caution: It is very easy to come up with a subgroup whose Cayley table, when the element names are used, is much to large to be readable on the screen.  If that is the case, `.translation().items()` may be a better route."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "D = SymmetricGroup(6)\n",
    "G = D.subgroup(['(1,5)','(4,6)'])\n",
    "T = G.cayley_table()\n",
    "names = []\n",
    "for item in G:\n",
    "    names.append(str(item))\n",
    "\n",
    "print(T.translation().items())\n",
    "T.change_names(names=names)\n",
    "print(T)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Lets look at another example."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "G = DihedralGroup(6); # the group of symmetries of the hexagon.\n",
    "\n",
    "# This is the element corresponding to rotation by 60 degrees\n",
    "tau = G('(1,2,3,4,5,6)'); \n",
    "\n",
    "# the gubgroup containing the six rotations about the origin.\n",
    "H = G.subgroup([tau]);\n",
    "\n",
    "# H is isomorphic to the cyclic group of order 6. \n",
    "print(\"Is H isomorphic to the cyclic group of order 6? \",H.is_isomorphic(CyclicPermutationGroup(6)))"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Finite Abelian Groups\n",
    "\n",
    "All finite Abelian groups take the form \n",
    "$$ \\mathbb{Z}_{n_1}\\times \\mathbb{Z}_{n_2}\\times\\cdots\\times \\mathbb{Z}_{n_m} $$\n",
    "for some $m\\in \\mathbb{N}$ and $n_i\\in\\mathbb{N}$ for all $i$. There is a good reason for this, as it will turn out, \n",
    "every finite abelian group is isomorphic to a group of this form. We can declare groups such as these as follows:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "G = AbelianGroup([5,4,3],names='a,b,c')"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Here we define the group\n",
    "$$ \\mathbb{Z}_5\\times \\mathbb{Z}_4\\times \\mathbb{Z}_3. $$\n",
    "The way that we have defined this group, we have the following relations $a^5=1$, $b^4=1$, and $c^3=1$.  Lets do some quick calculations in this group."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# define a, b, and c to be the generators of G\n",
    "(a,b,c) = G.gens(); \n",
    "\n",
    "# print the relations we explained above\n",
    "print(f\"a^5: {a^5}\\nb^4: {b^4}\\nc^3: {c^3}\")\n",
    "\n",
    "x = a^2*b^3*c\n",
    "y = a*b\n",
    "print(x*y)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Note that we define elements of these Abelian groups a little differently than we defined elements of other groups.  But this method does have the advantage of making it a little easier on us when dealing with the elements of this group.  \n",
    "\n",
    "To define a cyclic group, we can either use\n",
    "\n",
    "> AbelianGroup(n, names='x')\n",
    "\n",
    "or \n",
    "\n",
    "> CyclicPermutationGroup(n)\n",
    "\n",
    "**Note:** The first method does not allow us to use methods that are reserved for permutation groups, such as the `.is_isomorphic()` method, but the second does, since the second allows the group to be implemented as a permutation group."
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SageMath 9.7",
   "language": "sage",
   "name": "sagemath"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.10.8"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
